#
# Sample Queries for dblp RDF data library
#
# WF 2022-08-13 based on 
# https://ceur-ws.bitplan.com/index.php/Workdocumentation_2022-08-16#DBLP_versus_CEUR-WS_Queries
# by Tim
'CEUR-WS Papercount':
  sparql: |
    PREFIX dblp: <https://dblp.org/rdf/schema#>
    PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
    SELECT (COUNT(?paper) as ?count)
    WHERE { 
        ?proceeding dblp:publishedIn "CEUR Workshop Proceedings".
        ?paper dblp:publishedAsPartOf ?proceeding.
    }
    
'CEUR-WS Counts':
  sparql: |
    PREFIX dblp: <https://dblp.org/rdf/schema#>
    PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
    SELECT (COUNT(DISTINCT ?author) as ?numberOfAuthors) 
           (COUNT(DISTINCT ?paper) as ?numberOfPapers) 
           (COUNT(DISTINCT ?editor) as ?numberOfEditors)
           (COUNT(DISTINCT ?proceeding) as ?numberOfVolumes)
    WHERE { 
        ?proceeding dblp:publishedIn "CEUR Workshop Proceedings".
        OPTIONAL{?proceeding dblp:editedBy ?editor}
        OPTIONAL{
            ?paper dblp:publishedAsPartOf ?proceeding.
            OPTIONAL{?paper dblp:authoredBy ?author}
        }
    
    }
'CEUR-WS Paper Authors':
  sparql: |
    PREFIX datacite: <http://purl.org/spar/datacite/>
    PREFIX dblp: <https://dblp.org/rdf/schema#>
    PREFIX litre: <http://purl.org/spar/literal/>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
    SELECT
       ?dblp_author_id
       (SAMPLE(?_label) as ?label)
       (SAMPLE(?_wikidata_id) as ?wikidata_id)
       (SAMPLE(?_orcid_id) as ?orcid_id ) 
       (SAMPLE(?_gnd_id) as ?gnd_id ) 
    WHERE{
        ?proceeding dblp:publishedIn "CEUR Workshop Proceedings".
        ?paper dblp:publishedAsPartOf ?proceeding.
        ?paper dblp:title ?title .
        ?paper dblp:authoredBy ?dblp_author_id.
        OPTIONAL{ ?dblp_author_id rdfs:label ?_label}
        OPTIONAL{
            ?dblp_author_id datacite:hasIdentifier ?wd_blank.
            ?wd_blank datacite:usesIdentifierScheme datacite:wikidata.
            ?wd_blank litre:hasLiteralValue ?_wikidata_id.
        }
        OPTIONAL{
            ?dblp_author_id datacite:hasIdentifier ?orcid_blank.
            ?orcid_blank datacite:usesIdentifierScheme datacite:orcid.
            ?orcid_blank litre:hasLiteralValue ?_orcid_id.
        }
        OPTIONAL{
            ?dblp_author_id datacite:hasIdentifier ?gnd_blank.
            ?gnd_blank datacite:usesIdentifierScheme datacite:gnd.
            ?gnd_blank litre:hasLiteralValue ?_gnd_id.
        }
    }GROUP BY ?dblp_author_id
'CEUR-WS all Papers':
  sparql: |
    PREFIX datacite: <http://purl.org/spar/datacite/>
    PREFIX dblp: <https://dblp.org/rdf/schema#>
    PREFIX litre: <http://purl.org/spar/literal/>
    SELECT DISTINCT
       ?proceeding
       ?volume_number
       ?paper 
       (SAMPLE(?_title) as ?title)
       (SAMPLE(?_pdf_url) as ?pdf_url)
       (GROUP_CONCAT(?_author; SEPARATOR=";") as ?author)
      WHERE{
        ?proceeding dblp:publishedIn "CEUR Workshop Proceedings".
        ?proceeding dblp:publishedInSeriesVolume ?volume_number.
        ?paper dblp:publishedAsPartOf ?proceeding.
        OPTIONAL{?paper dblp:title ?_title .}
        OPTIONAL{?paper dblp:documentPage ?_pdf_url}
        OPTIONAL{?paper dblp:authoredBy ?_author}
    }
    GROUP BY ?proceeding ?volume_number ?paper
'CEUR-WS all Volumes':
  sparql: |
    PREFIX datacite: <http://purl.org/spar/datacite/>
    PREFIX dblp: <https://dblp.org/rdf/schema#>
    PREFIX litre: <http://purl.org/spar/literal/>
    SELECT DISTINCT
       ?proceeding
       ?volume_number
       (SAMPLE(?_title) as ?title)
       (GROUP_CONCAT(?_editor; SEPARATOR=";") as ?editor)
       (SAMPLE(?_dblp_event_id) as ?dblp_event_id)
      WHERE{
        ?proceeding dblp:publishedIn "CEUR Workshop Proceedings".
        ?proceeding dblp:publishedInSeriesVolume ?volume_number.
        OPTIONAL{?proceeding dblp:title ?_title .}
        OPTIONAL{?proceeding dblp:editedBy ?_editor}
        OPTIONAL{?proceeding dblp:listedOnTocPage ?_dblp_event_id}
    }
    GROUP BY ?proceeding ?volume_number
'CEUR-WS all Editors':
  sparql: |
    PREFIX datacite: <http://purl.org/spar/datacite/>
    PREFIX dblp: <https://dblp.org/rdf/schema#>
    PREFIX litre: <http://purl.org/spar/literal/>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
    SELECT
       ?dblp_author_id
       (SAMPLE(?_label) as ?label)
       (SAMPLE(?_wikidata_id) as ?wikidata_id)
       (SAMPLE(?_orcid_id) as ?orcid_id ) 
       (SAMPLE(?_gnd_id) as ?gnd_id ) 
    WHERE{
        ?proceeding dblp:publishedIn "CEUR Workshop Proceedings".
        ?proceeding dblp:editedBy ?dblp_author_id.
        OPTIONAL{ ?dblp_author_id rdfs:label ?_label}
        OPTIONAL{
            ?dblp_author_id datacite:hasIdentifier ?wd_blank.
            ?wd_blank datacite:usesIdentifierScheme datacite:wikidata.
            ?wd_blank litre:hasLiteralValue ?_wikidata_id.
        }
        OPTIONAL{
            ?dblp_author_id datacite:hasIdentifier ?orcid_blank.
            ?orcid_blank datacite:usesIdentifierScheme datacite:orcid.
            ?orcid_blank litre:hasLiteralValue ?_orcid_id.
        }
        OPTIONAL{
            ?dblp_author_id datacite:hasIdentifier ?gnd_blank.
            ?gnd_blank datacite:usesIdentifierScheme datacite:gnd.
            ?gnd_blank litre:hasLiteralValue ?_gnd_id.
        }
    }GROUP BY ?dblp_author_id